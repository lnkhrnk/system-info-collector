plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
}

group = 'com.example'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

application {
    mainClass = 'com.example.systeminfo.SystemInfoCollector'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    testImplementation platform('org.junit:junit-bom:5.10.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.14.2'

    // Используем современную версию
    checkstyle 'com.puppycrawl.tools:checkstyle:10.17.0'
}

test {
    useJUnitPlatform()
}

checkstyle {
    toolVersion = '10.17.0'
    // Берем файл из корня проекта
    configFile = file("checkstyle.xml")
    maxErrors = 0
    maxWarnings = 0
}


if (System.getProperty('os.name').toLowerCase().contains('windows')) {
    tasks.withType(Checkstyle) {
        enabled = false
    }
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}